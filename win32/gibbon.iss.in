[Setup]
AppName=Gibbon
AppVerName=Gibbon @VERSION@
DefaultDirName={pf}\Gibbon
DefaultGroupName=Gibbon
UninstallDisplayIcon={app}\gibbon.exe
Uninstallable=yes
AppPublisher=Guido Flohr
AppPublisherURL=http:\\gibbon.guido-flohr.net\
AppVersion=@VERSION@
OutputBaseFilename=gibbon-setup-@VERSION@-INSTALLERREVISION
LicenseFile=gibbon\share\doc\COPYING

[Components]
Name: "gtk"; Description: "GTK+ runtime environment"; Types: full compact custom; Flags: fixed
Name: "main"; Description: "Gibbon"; Types: full compact custom; Flags: fixed

Name: "locale"; Description: "Translations"; Types: full compact custom; Flags: fixed

[Tasks]
Name: common; Description: "For all users"; Components: main; Flags: exclusive
Name: user; Description: "For the current user only"; Components: main; Flags: exclusive unchecked
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Icons]
; Common task icons
Name: "{commonprograms}\{groupname}\Gibbon"; Filename: "{app}\bin\gibbon.exe"; Tasks: common
Name: "{commonprograms}\{groupname}\Uninstall Gibbon"; Filename: "{uninstallexe}"; Tasks: common
Name: "{commondesktop}\Gibbon"; Filename: "{app}\bin\gibbon.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\Gibbon"; Filename: "{app}\bin\gibbon.exe"; Tasks: quicklaunchicon

; User task icons
Name: "{userprograms}\{groupname}\Gibbon"; Filename: "{app}\bin\gibbon.exe"; Tasks: user
Name: "{userprograms}\{groupname}\Uninstall Gibbon"; Filename: "{uninstallexe}"; Tasks: user

[Files]

; All files
Source: "gtk\*"; DestDir: "{app}\"; Components: gtk; Flags: recursesubdirs
Source: "gibbon\*"; DestDir: "{app}\"; Components: main; Flags: recursesubdirs

[Run]
Filename: "{app}\bin\querymodules.bat"; StatusMsg: "Querying modules..."; Flags: runhidden

[Code]
{ Remove generated files not generated by the installer }

// Skip Components selection, all components are required anyway
function ShouldSkipPage(CurPageID: Integer): Boolean;
begin
	if CurPageID = wpSelectComponents then
		Result := True
	else
		Result := False;
end;
